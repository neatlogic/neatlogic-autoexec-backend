#!/usr/bin/perl

use FindBin;

use strict;
use Getopt::Long;
use File::Copy;
use File::Path;
use Cwd;

use DeployUtils;
use DeployLock;
use VerGet;
use ServerAdapter;

sub usage {
    my $pname = $FindBin::Script;

    print("usage: $pname [-v 0|1] [--envpath EnvPath] [--lang LANG] \n");
    print("              [--version VERSION] [--tagprefix TAGPREFIX\n");
    print("              [--username USERNAME] [--password PASSWORD]\n");
    print("\n");
    print("optional arguments:\n");
    print("  --envpath\toptionnal, name path for application\n");
    print("  --lang\toptionnal, LANG\n");
    print("  --version\toptionnal, Version number\n");
    print("  --tagprefix\toptionnal, Tag prefix, tag:[tagPrefix]version number\n");
    exit(1);
}

sub main {
    my ( $isHelp, $isVerbose, $lang, $envPath, $version );
    my ( $userName, $password );
    my $tagPrefix;
    GetOptions(
        'h|help'      => \$isHelp,
        'v|verbose=i' => \$isVerbose,
        'envpath=s'   => \$envPath,
        'lang=s'      => \$lang,
        'version=s'   => \$version,
        'tagprefix=s' => \$tagPrefix,
        'username=s'  => \$userName,
        'password=s'  => \$password
    );

    usage() if ( defined($isHelp) );

    if ( defined($lang) ) {
        $ENV{LANG}   = $lang;
        $ENV{LC_ALL} = $lang;
    }

    my $deployUtils = DeployUtils->new();
    my $buildEnv    = $deployUtils->deployInit( $envPath, $version );

    $envPath = $buildEnv->{NAME_PATH};
    $version = $buildEnv->{VERSION};

    my $optionError = 0;
    if ( not defined($envPath) or $envPath eq '' ) {
        $optionError = 1;
        print("ERROR: EnvPath not defined by option --envpath or Environment:NAME_PATH\n");
    }
    if ( not defined($version) or $version eq '' ) {
        $optionError = 1;
        print("ERROR: Version not defined by option --version or Environment:VERSION\n");
    }
    if ( $optionError == 1 ) {
        usage();
    }

    my $namePath = $buildEnv->{NAME_PATH};

    my $serverAdapter = ServerAdapter->new();

    my $lock = DeployLock->new($buildEnv);
    $lock->lockWorkspace($DeployLock::WRITE);
    my $verInfo = $serverAdapter->getVer($buildEnv);

    if ( not defined($verInfo) ) {
        print("ERROR: Can not find application $namePath version:$version.\n");
        return 3;
    }

    if ( defined($userName) and $userName ne '' ) {
        $verInfo->{username} = $userName;
    }
    if ( defined($password) and $password ne '' ) {
        $password = $deployUtils->decryptPwd($password);
        $verInfo->{password} = $password;
    }

    my $verGet = VerGet->new( $buildEnv, $verInfo, $isVerbose );
    my $ret    = $verGet->tag( $tagPrefix, $isVerbose );

    my $desc = "Create tag for $namePath version:$version";
    if ( $ret eq 0 ) {
        print("FINEST: $desc success.\n");
    }
    else {
        print("ERROR: $desc failed.\n");
    }

    return $ret;
}

exit main();

