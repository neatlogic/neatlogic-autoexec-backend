#!/usr/bin/perl
use FindBin;
use lib "$FindBin::Bin/../lib/perl-lib/lib/perl5";
use lib "$FindBin::Bin/../lib";

use strict;
use Getopt::Long;
use JSON;
use AutoExecUtils;
use SSHExpect;
use File::Spec;

sub usage {
    my $pname = $FindBin::Script;
    print("$pname --node <node> --protocol <protocol> --backupdir <backupdir path> --vendor <vendor> --timeout <timeout> \n");
    exit(1);
}

sub storeFile {
    my ( $path, $host, $result ) = @_;
    my $time = `date +%Y-%m-%d_%H:%M:%S`;
    chomp($time);
    my $dir = File::Spec->catfile( $path, $host );
    if ( !-e $dir ) {
        mkdir $dir;
    }
    my $fileName = File::Spec->catfile( $dir, "$time.txt" );
    open( my $fh, '>', $fileName ) or die "can't open $fileName $!";
    print($fh $result);
    close($fh);
    print("INFO::save backup file: $fileName done.\n");
}

sub defindVendor {
    my ($vendor) = @_;
    ###############################
    #configCmd : 查看交换机配置命令
    #fullPageCmd : 全屏打印（不出现 ----  More ----）
    ##############################
    my $vendorInfo = {
        "h3c"       => { "configCmd" => "display current-configuration", "fullPageCmd" => 'screen-length disable' },
        "cisco"     => { "configCmd" => "show run",                      "fullPageCmd" => 'terminal length 0' },
        "hillstone" => { "configCmd" => "show configuration",            "fullPageCmd" => 'terminal length 0' },
        "huawei"    => { "configCmd" => "display current-configuration", "fullPageCmd" => 'screen-length 0 temporary' }
    };
    my $info = $vendorInfo->{$vendor};
    return $info;
}

sub main {
    $| = 1;
    AutoExecUtils::setEnv();
    my ( $ishelp, $isVerbose );
    my ( $node, $vendor, $protocol, $backupdir, $timeout, $exitCmd );

    usage() if ( defined($ishelp) );
    $isVerbose = 0;

    GetOptions(
        'help'        => \$ishelp,
        'node:s'      => \$node,
        'protocol:s'  => \$protocol,
        'vendor:s'    => \$vendor,
        'backupdir:s' => \$backupdir,
        'timeout:i'   => \$timeout,
        'v|verbose=i' => \$isVerbose,
        'exitCmd:s'   => \$exitCmd
    );

    my $nodeInfo  = {};
    my $hasOptErr = 0;
    if ( not defined($node) ) {
        $node = $ENV{AUTOEXEC_NODE};
    }

    if ( not defined($node) or $node eq '' or not defined($vendor) or $vendor eq '' ) {
        $hasOptErr = 1;
    }
    else {
        $nodeInfo = from_json($node);
    }

    if ( not defined($vendor) or $vendor eq '' ) {
        print("ERROR: Must define vendor by option --vendor.\n");
        $hasOptErr = 1;
    }

    if ( not defined($backupdir) or $backupdir eq '' ) {
        print("ERROR: Must define backup directory by option --backupdir.\n");
        $hasOptErr = 1;
    }

    if ( $backupdir =~ /\/\.\.\// or $backupdir =~ /^\.\.\// or $backupdir =~ /\/\.\.$/ ) {
        print("ERROR: backup directory path can not has parent dir opertor:\"..\".\n");
        $hasOptErr = 1;
    }

    if ( !-d $backupdir ) {
        print("ERROR: backup directory not exists.\n");
        $hasOptErr = 1;
    }

    if ( not defined($protocol) or $protocol eq '' ) {
        $protocol = 'ssh';
    }

    if ( not defined($timeout) or $timeout == 0 ) {
        $timeout = 10;
    }

    if ( not defined($exitCmd) or $exitCmd eq '' ) {
        $exitCmd = 'exit';
    }

    if ( $hasOptErr == 1 ) {
        usage();
    }

    my $vendorInfo = defindVendor($vendor);
    if ( not defined($vendorInfo) ) {
        print("ERROR:: $vendor model not support .\n");
        exit(1);
    }

    my ( $host, $port, $username, $password );
    $host     = $nodeInfo->{'host'};
    $port     = $nodeInfo->{'protocolPort'};
    $username = $nodeInfo->{'username'};
    $password = $nodeInfo->{'password'};

    my $fullPageCmd = $vendorInfo->{fullPageCmd};
    my $configCmd   = $vendorInfo->{configCmd};

    print("INFO::config back start.\n");
    my $sep = SSHExpect->new(
        host     => $host,
        port     => $port,
        protocol => $protocol,
        username => $username,
        password => $password,
        timeout  => $timeout,
        verbose  => $isVerbose
    );
    my $spawn = $sep->login();

    if ( not defined($spawn) ) {
        print("ERROR:: $vendor config backup failed.\n");
        exit(1);
    }
    else {
        my $rsConfig = $sep->backup( $fullPageCmd, $configCmd, $exitCmd );
        storeFile( $backupdir, $host, $rsConfig );
        $sep->close($exitCmd);
        print("INFO:: $vendor config backup success.\n");
        exit(0);
    }
}

exit main();

