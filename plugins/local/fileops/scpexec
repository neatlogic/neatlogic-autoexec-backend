#!/usr/bin/perl

use FindBin;
use lib "$FindBin::Bin/../lib/perl-lib/lib/perl5";
use lib "$FindBin::Bin/../lib";

use strict;
use Getopt::Long;
use File::Glob qw(bsd_glob);
use SCPExec;
use AutoExecUtils;
use Cwd;

sub usage {
    my $pname = $FindBin::Script;

    print("Usage: $pname [--verbose 0|1] [--node NodeJson] [--direction push|pull]\n");
    print("              [--np NoPermission] [--src SourceDirectory] [--dest DestinationDirectory] \n");
    print("\n");
    print("       --node:      Host node json\n");
    print("       --direction: push from local site to remote site or pull from remote site to local site .\n");
    print("       --np:        not preserve the mode of file\n");
    print("       --src:       copy source path ,many src path , separated .\n");
    print("       --dest:      remote target path .\n");
    exit(1);
}

sub main {
    my ( $isHelp, $isVerbose );
    my ( $node, $direction, $src, $dest, $np );
    AutoExecUtils::setEnv();

    my $pname = $FindBin::Script;
    $isVerbose = 0;
    $np        = 0;

    GetOptions(
        'h|help'      => \$isHelp,
        'v|verbose=i' => \$isVerbose,
        'node=s'      => \$node,
        'np=i'        => \$np,
        'direction:s' => \$direction,
        'src:s'       => \$src,
        'dest:s'      => \$dest
    );

    usage() if ( defined($isHelp) );

    my $optionError = 0;
    my $nodeInfo    = {};
    if ( not defined($node) ) {
        $node = $ENV{AUTOEXEC_NODE};
    }

    if ( not defined($node) or $node eq '' ) {
        $optionError = 1;
    }
    else {
        $nodeInfo = from_json($node);
    }

    if ( not defined($direction) or $direction eq '' ) {
        print("ERROR: must define direction value pull or push .\n");
        $optionError = 1;
    }

    if ( not defined($src) or $src eq '' ) {
        print("ERROR: must define src path.\n");
        $optionError = 1;
    }
    if ( $src =~ /\/\.\.\// or $src =~ /^\.\.\// or $src =~ /\/\.\.$/ ) {
        print("ERROR: src path can not has parent dir opertor:\"..\".\n");
        $optionError = 1;
    }

    if ( not defined($dest) or $dest eq '' ) {
        print("ERROR: must define src path.\n");
        $optionError = 1;
    }
    if ( $dest =~ /\/\.\.\// or $dest =~ /^\.\.\// or $dest =~ /\/\.\.$/ ) {
        print("ERROR: dest path can not has parent dir opertor:\"..\".\n");
        $optionError = 1;
    }

    if ( $optionError == 1 ) {
        usage();
    }

    my $host    = $nodeInfo->{host};
    my $port    = $nodeInfo->{protocolPort};
    my $user    = $nodeInfo->{username};
    my $pass    = $nodeInfo->{password};
    my $insId   = $nodeInfo->{resourceId};
    my $insName = $nodeInfo->{nodeName};

    if ( not defined($port) ) {
        $port = 22;
    }

    my $isSuccess = 0;
    my @src_dirs  = split( /,/, $src );
    my $src_path  = '';
    if ( $direction eq 'push' ) {
        foreach my $dir (@src_dirs) {
            $src_path = "$src_path $dir";
        }
        $dest = "$user\@$host:$dest";
        $isSuccess = myExec( host => $host, port => $port, user => $user, pass => $pass, src => $src_path, dest => $dest, isVerbose => $isVerbose, notpreserve => $np );
    }
    elsif ( $direction eq 'pull' ) {
        foreach my $dir (@src_dirs) {
            $src_path = "$user\@$host:$dir";
            $isSuccess = myExec( host => $host, port => $port, user => $user, pass => $pass, src => $src_path, dest => $dest, isVerbose => $isVerbose, notpreserve => $np );
            if ( $isSuccess ne 0 ) {
                last;
            }
        }
    }

    if ( $isSuccess ne 0 ) {
        print("ERROR: $pname --$direction $src $dest failed.\n");
    }
    else {
        print("INFO: $pname --$direction $src $dest success.\n");
    }
    return $isSuccess;
}

sub myExec {
    my (%args)    = @_;
    my $host      = $args{host};
    my $port      = $args{port};
    my $user      = $args{user};
    my $pass      = $args{pass};
    my $src       = $args{src};
    my $dest      = $args{dest};
    my $isVerbose = $args{isVerbose};
    my $np        = $args{notpreserve};
    my $scpExec = new SCPExec( host => $host, port => $port, user => $user, pass => $pass, src => $src, dest => $dest, isVerbose => $isVerbose, notpreserve => $np );
    my $rc = $scpExec->exec();
    return $rc;
}

exit( main() );
