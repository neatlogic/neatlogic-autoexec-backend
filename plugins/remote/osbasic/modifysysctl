#!/usr/bin/perl
use FindBin;
use lib "$FindBin::Bin/../lib/perl-lib/lib/perl5";
use lib "$FindBin::Bin/../lib";

use strict;
use POSIX qw(strftime);
use IO::File;
use JSON;
use Getopt::Long;

sub usage {
    my $pname = $FindBin::Script;

    print("$pname --backup 0|1 --content <sysctl.conf content>\n");
    exit(1);
}

sub backup {
    my ($filePath)  = @_;
    my $dateTimeStr = strftime( "%Y%m%d-%H%M%S", localtime() );
    my $exitCode    = system(qq{cp "$filePath" "$filePath.$dateTimeStr"});
    return $exitCode;
}

sub getShmAllAndMax {
    my $pageSizeTxt = `getconf PAGESIZE`;
    my $pageSize    = int($pageSizeTxt);

    #MemTotal:        3880620 kB
    #Mem in KB
    my $memTotalTxt = `grep MemTotal /proc/meminfo`;
    my $memTotal    = int($memTotalTxt);
    if ( $memTotalTxt =~ /(\d+)[^\d]+kB/i ) {
        $memTotal = int($1) * 1024;
    }
    elsif ( $memTotalTxt =~ /(\d+)[^\d]+MB/i ) {
        $memTotal = int($1) * 1024 * 1024;
    }
    elsif ( $memTotalTxt =~ /(\d+)[^\d]+GB/i ) {
        $memTotal = int($1) * 1024 * 1024 * 1024;
    }

    my $shmall = $memTotal / 2 / $pageSize;
    my $shmmax = $memTotal / 2 - 1;

    return ( $shmall, $shmmax );
}

sub main {
    $| = 1;    #不对输出进行buffer，便于实时看到输出日志
    my $filePath = '/etc/sysctl.conf';

    my $needBackup = 0;
    my $autoSetShm = 0;
    my $content;

    GetOptions(
        'backup=i'     => \$needBackup,
        'autosetshm=i' => \$autoSetShm,
        'content=s'    => \$content
    );

    if ( not defined($content) or $content eq '' ) {
        print("ERROR: Must defined modify content by option --content\n");
        usage();
    }

    my $hasError = 0;

    my $scriptDir = $FindBin::Bin;
    chdir($scriptDir);

    my $confMap = {};
    $content =~ s/\\n/\n/sg;
    foreach my $line ( split( /\n/, $content ) ) {
        $line =~ s/^\s*|\s*$//g;
        my ( $key, $val ) = split( /\s*=\s*/, $line );
        $confMap->{$key} = $val;
    }

    my ( $shmall, $shmmax );
    if ( $autoSetShm == 1 ) {
        ( $shmall, $shmmax ) = getShmAllAndMax();
        $confMap->{'kernel.shmall'} = $shmall;
        $confMap->{'kernel.shmmax'} = $shmmax;
    }

    my $isChanged  = 0;
    my $newContent = '';
    my $fh         = IO::File->new("+<$filePath");
    if ( defined($fh) ) {
        my $line;
        while ( $line = $fh->getline() ) {
            if ( $line !~ /^\s*$/ and $line !~ /^\s*#/ ) {
                my $tmpLine = $line;
                $tmpLine =~ s/^\s*|\s*$//g;
                my ( $key, $val ) = split( /\s*=\s*/, $tmpLine );
                my $newVal = $confMap->{$key};
                if ( defined($newVal) ) {
                    delete( $confMap->{$key} );
                    if ( $newVal ne $val ) {
                        $isChanged = 1;
                        $line =~ s/\s*=.*$/ = $newVal/;
                        print("Replace: $tmpLine -> $line");
                    }
                    else {
                        print("NotChanged: $line");
                    }
                }
            }
            $newContent = $newContent . $line;
        }

        while ( my ( $key, $val ) = each(%$confMap) ) {
            $isChanged  = 1;
            $newContent = $newContent . "$key = $val\n";
            print("Append: $key = $val\n");
        }

        if ( $isChanged == 1 ) {
            if ( $needBackup == 1 ) {
                my $exitCode = backup($filePath);
                if ( $exitCode != 0 ) {
                    print("ERROR: Backup file:$filePath failed, $!\n");
                    $hasError = 2;
                }
            }

            if ( $hasError == 0 ) {
                $fh->seek( 0, 0 );
                $fh->truncate(0);
                $fh->write( $newContent, length($newContent) );
            }
        }

        $fh->close();
    }
    else {
        $hasError = 1;
        print("ERROR: Can not open file:$filePath, $!\n");
    }

    return $hasError;
}

exit main();
