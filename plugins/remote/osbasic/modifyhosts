#!/usr/bin/perl
use FindBin;
use lib "$FindBin::Bin/../lib/perl-lib/lib/perl5";
use lib "$FindBin::Bin/../lib";

use strict;
use POSIX qw(uname strftime);
use IO::File;
use JSON;
use Getopt::Long;
Getopt::Long::Configure("pass_through");

use AutoExecUtils;

sub usage {
    my $pname = $FindBin::Script;

    print("$pname --backup 0|1 <hosts content> --content <modify content>\n");
    exit(1);
}

sub backup {
    my ($filePath)  = @_;
    my $dateTimeStr = strftime( "%Y%m%d-%H%M%S", localtime() );
    my $exitCode    = system(qq{cp "$filePath" "$filePath.$dateTimeStr"});
    return $exitCode;
}

sub main {
    $| = 1;    #不对输出进行buffer，便于实时看到输出日志
    my $filePath = '/etc/hosts';

    my $osType = ( uname() )[0];
    $osType =~ s/\s.*$//;
    if ( $osType eq 'Windows' ) {
        my $winDir = $ENV{WINDIR};
        $winDir =~ s/\\/\//g;
        $filePath = "$winDir/system32/drivers/etc/hosts";
    }

    my $needBackup = 0;
    my $contentJson;

    GetOptions(
        'backup:i'  => \$needBackup,
        'content:s' => \$contentJson
    );

    if ( not defined($contentJson) or $contentJson eq '' ) {
        print("ERROR: Must defined modify content by option --content\n");
        usage();
    }

    my $hasError = 0;

    my $scriptDir = $FindBin::Bin;
    chdir($scriptDir);

    my $confMap    = {};
    my $contentObj = from_json($contentJson);
    foreach my $line ( split( /\n/, $contentObj->{content} ) ) {
        $line =~ s/^\s*|\s*$//g;
        my ( $ip, $name ) = split( /\s+/, $line, 2 );
        $confMap->{$ip} = $name;
    }

    my $isChanged  = 0;
    my $newContent = '';
    my $fh         = IO::File->new("+<$filePath");
    if ( defined($fh) ) {
        my $line;
        while ( $line = $fh->getline() ) {
            if ( $line !~ /^\s*$/ and $line !~ /^\s*#/ ) {
                my $tmpLine = $line;
                $tmpLine =~ s/^\s*|\s*$//g;
                my ( $ip, $name ) = split( /\s+/, $tmpLine, 2 );
                my $newName = $confMap->{$ip};
                if ( defined($newName) ) {
                    delete( $confMap->{$ip} );
                    if ( $newName ne $name ) {
                        $isChanged = 1;
                        $line =~ s/\s+.*$/\t$newName/;
                        print("Replace: $tmpLine -> $line");
                    }
                    else {
                        print("NotChanged: $line");
                    }
                }
            }
            $newContent = $newContent . $line;
        }

        while ( my ( $ip, $name ) = each(%$confMap) ) {
            $isChanged  = 1;
            $newContent = $newContent . "$ip\t$name\n";
            print("Append: $ip\t$name\n");
        }

        if ( $isChanged == 1 ) {
            if ( $needBackup == 1 ) {
                my $exitCode = backup($filePath);
                if ( $exitCode != 0 ) {
                    print("ERROR: Backup file:$filePath failed, $!\n");
                    $hasError = 2;
                }
            }

            if ( $hasError == 0 ) {
                $fh->seek( 0, 0 );
                $fh->truncate(0);
                $fh->write( $newContent, length($newContent) );

                my $out = { hostsConf => $newContent };
                AutoExecUtils::saveOutput($out);
            }
        }

        $fh->close();
    }
    else {
        $hasError = 1;
        print("ERROR: Can not open file:$filePath, $!\n");
    }

    return $hasError;
}

exit main();
